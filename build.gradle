plugins {
    id 'maven-publish'
    id 'net.fabricmc.fabric-loom-remap' version '1.15.+'
    id 'ploceus' version '1.15-SNAPSHOT'
    id "com.modrinth.minotaur" version "2.+"
}

ploceus {
	setIntermediaryGeneration(2)
}

version = property("mod_version")
group = property("maven_group")

base {
    archivesName = project.archives_base_name
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    
    withSourcesJar()
}

loom {
    if (project.hasProperty("accesswidener_name") && project.accesswidener_name.length() > 0) {
        accessWidenerPath = file("src/main/resources/${project.accesswidener_name}.accesswidener")
    }
    mixin {
        useLegacyMixinAp = false
    }
}
repositories {
    maven { 
        name = "Casa De Periut"
        url = "https://matthewperiut.github.io/repository" 
    }
    
    maven {
        name = "Glass Releases"
        url = "https://maven.glass-launcher.net/releases"
    }

    maven {
        name = "Glass Snapshots"
        url = "https://maven.glass-launcher.net/snapshots"
    }

    maven {
        name = "NyaRepo"
        url = "https://maven.fildand.cz/releases"
    }

    maven {
        name = "Ornithe Releases"
        url = "https://maven.ornithemc.net/releases" 
    }
    
    maven {
        name = "Froge Maven"
        url = "https://maven.minecraftforge.net/"
    }

    maven {
        name = "Jitpack"
        url = "https://jitpack.io"
    }

    // Modrinth Maven
    exclusiveContent {
        forRepository {
            //noinspection ForeignDelegate
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }

    mavenCentral()
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings(ploceus.mappings("net.glasslauncher:biny-ornithe:b1.7.3+build.${project.biny_mappings}:mergedv2"))

    exceptions ploceus.raven(project.raven_build)
    signatures ploceus.sparrow(project.sparrow_build)
	clientNests("net.glasslauncher:biny-nests:b1.7.3-client+build.${project.nests_build}")
	serverNests("net.glasslauncher:biny-nests:b1.7.3-server+build.${project.nests_build}")
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    implementation "org.slf4j:slf4j-api:1.8.0-beta4"
    implementation "org.apache.logging.log4j:log4j-slf4j18-impl:2.17.2"
    implementation "me.carleslc:Simple-Yaml:1.8.4"
    implementation "com.google.guava:guava:33.2.1-jre"
    implementation "com.google.code.gson:gson:2.9.0"

    // Exclude Legacy Fabric LWJGL 2 wrapper (starac provides LWJGL 3)
    configurations.configureEach {
        exclude group: 'org.lwjgl.lwjgl'
    }

    modImplementation ("com.periut:starac:${project.starac_version}")
    
    modImplementation ("net.modificationstation:StationAPI:${project.stapi_version}") {
        exclude group: "babric", module: "fabric-loader"
    }

//    if (project.hasProperty("modmenu_version")) {
//        modImplementation("net.danygames2014:modmenu:${project.modmenu_version}") {
//            transitive = false
//        }
//    }

    if (project.hasProperty("gcapi_version")) {
        modImplementation("net.glasslauncher.mods:GlassConfigAPI:${project.gcapi_version}") {
            transitive = false
        }
    }

//    if (project.hasProperty("alwaysmoreitems_version")) {
//        modImplementation("net.glasslauncher.mods:AlwaysMoreItems:${project.alwaysmoreitems_version}") {
//            transitive = false
//        }
//    }

//    if (project.hasProperty("retrocommands_version")) {
//        modImplementation("maven.modrinth:retrocommands:${project.retrocommands_version}-b1.7.3") {
//            transitive = false
//        }
//    }

//    if (project.hasProperty("spawneggs_version")) {
//        modImplementation("net.danygames2014:spawneggs:${project.spawneggs_version}") {
//            transitive = false
//        }
//    }

//    if (project.hasProperty("bhcreative_version")) {
//        modImplementation("maven.modrinth:bh-creative:${project.bhcreative_version}-b1.7.3") {
//            transitive = false
//        }
//    }

//    if (project.hasProperty("accessoryapi_version")) {
//        modImplementation("maven.modrinth:accessory-api:${project.accessoryapi_version}") {
//            transitive = false
//        }
//    }

//    if (project.hasProperty("whatsthis_version")) {
//        modImplementation("net.danygames2014:WhatsThis:${project.whatsthis_version}") {
//            transitive = false
//        }
//    }

//    if (project.hasProperty("nyalib_version")) {
//        modImplementation("net.danygames2014:NyaLib:${project.nyalib_version}")
//    }

//    if (project.hasProperty("uniwrench_version")) {
//        modImplementation("net.danygames2014:UniWrench:${project.uniwrench_version}") {
//            transitive = false
//        }
//    }

//    if (project.hasProperty("debugutilities_version")) {
//        modImplementation("net.danygames2014:DebugUtilities:${project.debugutilities_version}")
//    }

    if (project.hasProperty("retroauth_version")) {
        modRuntimeOnly("maven.modrinth:retroauth:${project.retroauth_version}-b1.7.3") {
            transitive = false
        }
    }

//    if (project.hasProperty("unitweaks_version")) {
//        modRuntimeOnly("maven.modrinth:unitweaks:${project.unitweaks_version}-b1.7.3") {
//            transitive = false
//        }
//    }

//     modRuntimeOnly("maven.modrinth:stationapis-no-startup-screen:3.0-b1.7.3") {
//     transitive = false
//     }
}

configurations.configureEach {
    exclude group: "net.glasslauncher.mods", module: "ModMenu"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

// Disables the generation of the .module file
tasks.withType(GenerateModuleMetadata).configureEach {
    enabled = false
}

// Gradle 9 fails the build if there are no tests, but we are using the test package for a test mod
tasks.withType(Test).configureEach {
    failOnNoDiscoveredTests = false
}

jar {
    from("LICENSE") {
        rename { "${it}_" + base.archivesName.get() }
    }
}

// Maven Publishing
publishing {
    repositories {
        mavenLocal()

        // Glass Maven
        if (project.hasProperty("calmilamsy_secret_publishing_cave_username")) {
            maven {
                url = "https://maven.glass-launcher.net/releases"
                credentials {
                    username = "${project.calmilamsy_secret_publishing_cave_username}"
                    password = "${project.calmilamsy_secret_publishing_cave_password}"
                }
            }
        }

        // NyaRepo
        if (project.hasProperty("nyarepo_publishing_cave_username")) {
            maven {
                url = "https://maven.fildand.cz/releases"
                credentials {
                    username = "${project.nyarepo_publishing_cave_username}"
                    password = "${project.nyarepo_publishing_cave_password}"
                }
            }
        }
    }

    publications {
        create("mavenJava", MavenPublication) {
            //noinspection GrUnresolvedAccess
            artifactId = base.archivesName.get()
            //noinspection GrUnresolvedAccess, GroovyAssignabilityCheck
            from components.java
        }
    }
}

// Modrinth Publishing
modrinth {
    if (project.hasProperty("modrinth_publishing_cave")) {
        token = project.modrinth_publishing_cave
    }
    projectId = project.modrinth_slug
    versionNumber = project.mod_version
    versionName = "${project.modrinth_version_name} ${project.mod_version}"
    changelog = rootProject.file("CHANGELOG.md").text
    versionType = "release"
    uploadFile = remapJar
    gameVersions = ["b1.7.3"]
    loaders = ["fabric", "babric"]
    dependencies {
        required.project "stationapi"
        required.project "glass-config-api"
    }
}

tasks.named("modrinth") {
    onlyIf {
        if (!project.hasProperty("modrinth_publishing_cave") || !project.hasProperty("modrinth_slug") || project.modrinth_slug.length() == 0 || !project.hasProperty("modrinth_version_name") || project.modrinth_version_name.length() == 0) {
            println("Modrinth publishing skipped due to the required properties not being set")
            return false
        }

        return true
    }
}

tasks.register("update") {
    doLast {
        def propsFile = file("gradle.properties")

        List<String> lines = propsFile.readLines()

        updateDependency(lines, "BINY", "yarn_mappings", "https://maven.glass-launcher.net/releases/net/glasslauncher/biny")
        updateDependency(lines, "Fabric Loader", "loader_version", "https://maven.fabricmc.net/net/fabricmc/fabric-loader/")
        updateDependency(lines, "StationAPI", "stapi_version", "https://maven.glass-launcher.net/releases/net/modificationstation/StationAPI")
        updateDependency(lines, "Glass Networking", "glass_networking_version", "https://maven.glass-launcher.net/releases/net/glasslauncher/mods/glass-networking")
        updateDependency(lines, "Glass Config API", "gcapi_version", "https://maven.glass-launcher.net/releases/net/glasslauncher/mods/GlassConfigAPI")
        updateDependency(lines, "Always More Items", "alwaysmoreitems_version", "https://maven.glass-launcher.net/releases/net/glasslauncher/mods/AlwaysMoreItems")
        updateDependency(lines, "ModMenu", "modmenu_version", "https://maven.glass-launcher.net/releases/net/danygames2014/modmenu")
        updateDependency(lines, "BH Creative", "bhcreative_version", "https://api.modrinth.com/maven/maven/modrinth/bh-creative")
        updateDependency(lines, "Spawn Eggs", "spawneggs_version", "https://maven.glass-launcher.net/releases/net/danygames2014/spawneggs")
        updateDependency(lines, "Retro Commands", "retrocommands_version", "https://api.modrinth.com/maven/maven/modrinth/retrocommands")
        updateDependency(lines, "Accessory API", "accessoryapi_version", "https://api.modrinth.com/maven/maven/modrinth/accessory-api")
        updateDependency(lines, "RetroAuth", "retroauth_version", "https://api.modrinth.com/maven/maven/modrinth/retroauth")
        updateDependency(lines, "UniTweaks", "unitweaks_version", "https://api.modrinth.com/maven/maven/modrinth/unitweaks")
        updateDependency(lines, "Whats This", "whatsthis_version", "https://maven.glass-launcher.net/releases/net/danygames2014/WhatsThis")
        updateDependency(lines, "NyaLib", "nyalib_version", "https://maven.glass-launcher.net/releases/net/danygames2014/NyaLib")
        updateDependency(lines, "UniWrench", "uniwrench_version", "https://maven.glass-launcher.net/releases/net/danygames2014/UniWrench")
        updateDependency(lines, "DebugUtilities", "debugutilities_version", "https://maven.glass-launcher.net/releases/net/danygames2014/DebugUtilities")

        propsFile.text = lines.join(System.lineSeparator())
        println("Finished updating dependencies, please reload your Gradle Project")
    }
}

def updateDependency(List<String> lines, String name, String property, String artifactUrl) {
    println("Checking updates for " + name)

    if (!project.hasProperty(property)) {
        println("Project does not have property " + property)
        return
    }

    String latestVersion = fetchLatestVersion(artifactUrl)

    if (latestVersion == null) {
        println("Error while fetching latest version")
        return
    }

    for (int i = 0; i < lines.size(); i++) {
        if (lines[i].contains(property)) {
            String[] split = lines[i].split("=")
            String currentVersion = split[1]

            if (!currentVersion.equalsIgnoreCase(latestVersion)) {
                println("Dependency " + name + " updated. (" + currentVersion + " -> " + latestVersion + ")")
                lines[i] = split[0] + "=" + latestVersion
            } else {
                println("Dependency " + name + " is already up to date. (" + currentVersion + ")")
            }

            break
        }
    }
}

static def fetchLatestVersion(String artifactUrl) {
    String latestVersion

    try {
        String artifactMeta = new URL(artifactUrl + "/maven-metadata.xml").text
        latestVersion = artifactMeta.split("<latest>")[1].split("</latest>")[0]
    } catch (Exception e) {
        println("Error while updating dependency " + artifactUrl)
        println(e.message)
        return null
    }

    return latestVersion
}